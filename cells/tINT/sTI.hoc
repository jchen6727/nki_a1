// FROM https://senselab.med.yale.edu/ModelDB/showmodel.cshtml?model=116862&file=/b09jan13/batch_.hoc#tabs-2
//* 2 CMP Thalamic interneuron (tTI)
//** begintemplate tTI --> sTI now (not sure why s or T // find out)

begintemplate sTI
  public soma,dend
  public nmda, ampa, gabaa, gabab //p, inhib, excit, inj
  //public up
  //external fgabaa,fgabab, fampa, fnmda, ftc, fre, finj, fgen
  objectvar nmda, ampa, gabaa, gabab //inj
  //objref this,up

create soma, dend


proc init () { local i,j
    create soma, dend
  
    soma {
        gabaa = new GABAA(0.5)
        gabab = new GABAB(0.5)
        ampa = new AMPA(0.5)
        nmda = new NMDA(0.5)
      }
    forsec "soma" {
        nseg = 1
        diam = 10
        L = 16
        cm = 1

        insert naf2
        gmax_naf2     =  0.06
        mvhalf_naf2   = -40
        mvalence_naf2 =  5
        hvhalf_naf2   = -43
        hvalence_naf2 = -6

        insert kdr2
        gmax_kdr2     =  0.07
        mvhalf_kdr2   = -31
        mvalence_kdr2 =  3.8


        insert Pass // ntleak.mod
        g_Pass = 0.00015 //ORIG from 15-cmp model: 18e-6
        erev_Pass = -75 //ORIG from 15-cmp model: -72.5
      
        insert ical //IL.mod
        pcabar_ical = 9.0e-4
        sh1_ical = -10
        sh2_ical = 0
      
        insert ical3 //IL3.mod
        pcabar_ical3 = 9.0e-4
        sh1_ical3 = -10
        sh2_ical3 = 0
      
        insert it2 // ntt.mod
        gcabar_it2 = 4.0e-4
        shift1_it2 = 7
        shift2_it2 = 0
        mx_it2 = 3.0
        hx_it2 = 1.5
        sm_it2 = 4.8
        sh_it2 = 4.6
      
        insert cad //cp.mod
        kt_cad = 0e-6
        kt2_cad = 0e-7
        k_cad  = 7.5e-3
        taur_cad  = 150
        cainf_cad  = 1e-8
        taur2_cad  = 80
        cainf2_cad  = 5.2e-5
        caix_cad  = 5.0e-5
      
        insert Cad //cp2.mod
        kt_Cad = 0e-6
        k_Cad  = 7.5e-3
        taur_Cad  = 150
        Cainf_Cad  = 1e-8
        taur2_Cad  = 80
        Cainf2_Cad  = 5.2e-5
        Caix_Cad  = 5.0e-5
      
        insert iahp
        gkbar_iahp = 0.4
        beta_iahp = 0.02
        cac_iahp = 0.8e-3
        rat_iahp = 0.2
      
        insert ican
        gbar_ican =2e-5
        beta_ican = 0.003
        cac_ican = 1.1e-4
        rat_ican = 0.1
        x_ican = 8
      
        insert iar // ntlh.mod
        ghbar_iar = 1.3e-4
        shift_iar = -0.0
        erev_iar = -44
        stp_iar = 7.4
      }

  forsec "dend" {
        nseg = 1
        diam = 50
        L = 50
        cm = 3

        insert Pass
        g_Pass = 8e-6
        erev_Pass = -72.5
      }
    connect dend(0), soma (1)
    forall Ra = 50
  }
endtemplate sTI

